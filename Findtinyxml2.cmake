# tinyxml2 Conan package
# Dmitriy Vetutnev, Odant, 2019


find_path(TINYXML2_INCLUDE_DIR
    NAMES tinyxml2.h
    PATHS ${CONAN_INCLUDE_DIRS_TINYXML2}
    NO_DEFAULT_PATH
)

find_library(TINYXML2_LIBRARY
    NAMES tinyxml2 tinyxml2d
    PATHS ${CONAN_LIB_DIRS_TINYXML2}
    NO_DEFAULT_PATH
)


if(TINYXML2_INCLUDE_DIR AND EXISTS ${TINYXML2_INCLUDE_DIR}/tinyxml2.h)
    file(STRINGS ${TINYXML2_INCLUDE_DIR}/tinyxml2.h DEFINE_TINYXML2_MAJOR_VERSION REGEX "^#define TINYXML2_MAJOR_VERSION [0-9]+")
    string(REGEX REPLACE "^#define TINYXML2_MAJOR_VERSION ([0-9]+)" "\\1" TINYXML2_VERSION_MAJOR ${DEFINE_TINYXML2_MAJOR_VERSION})
    file(STRINGS ${TINYXML2_INCLUDE_DIR}/tinyxml2.h DEFINE_TINYXML2_MINOR_VERSION REGEX "^#define TINYXML2_MINOR_VERSION [0-9]+")
    string(REGEX REPLACE "^#define TINYXML2_MINOR_VERSION ([0-9]+)" "\\1" TINYXML2_VERSION_MINOR ${DEFINE_TINYXML2_MINOR_VERSION})
    file(STRINGS ${TINYXML2_INCLUDE_DIR}/tinyxml2.h DEFINE_TINYXML2_PATCH_VERSION REGEX "^#define TINYXML2_PATCH_VERSION [0-9]+")
    string(REGEX REPLACE "^#define TINYXML2_PATCH_VERSION ([0-9]+)" "\\1" TINYXML2_VERSION_PATCH ${DEFINE_TINYXML2_PATCH_VERSION})
    set(TINYXML2_VERSION_STRING "${TINYXML2_VERSION_MAJOR}.${TINYXML2_VERSION_MINOR}.${TINYXML2_VERSION_PATCH}")
    unset(DEFINE_TINYXML2_MAJOR_VERSION)
    unset(DEFINE_TINYXML2_MINOR_VERSION)
    unset(DEFINE_TINYXML2_PATCH_VERSION)
endif()

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(tinyxml2
    REQUIRED_VARS TINYXML2_LIBRARY TINYXML2_INCLUDE_DIR
    VERSION_VAR TINYXML2_VERSION_STRING
)


if(TINYXML2_FOUND)
    set(TINYXML2_INCLUDE_DIRS ${TINYXML2_INCLUDE_DIR})
    set(TINYXML2_LIBRARIES ${TINYXML2_LIBRARY})
    mark_as_advanced(TINYXML2_INCLUDE_DIR TINYXML2_LIBRARY)
    set(TINYXML2_DEFINITIONS ${CONAN_COMPILE_DEFINITIONS_TINYXML2}) # Add defines from package_info

    if(NOT TARGET tinyxml2::tinyxml2)
        add_library(tinyxml2::tinyxml2 UNKNOWN IMPORTED)
        set_target_properties(tinyxml2::tinyxml2 PROPERTIES
            INTERFACE_INCLUDE_DIRECTORIES ${TINYXML2_INCLUDE_DIR}
            IMPORTED_LOCATION ${TINYXML2_LIBRARY}
        )
    endif()
endif()
